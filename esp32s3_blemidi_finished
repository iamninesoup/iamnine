#include <BLEDevice.h>
#include <BLEUtils.h>
#include <BLEServer.h>
#include <BLE2902.h>

#define SERVICE_UUID        "03b80e5a-ede8-4b33-a751-6ce34ec4c700"
#define CHARACTERISTIC_UUID "7772e5db-3868-4112-a1a9-f2669d106bf3"

BLECharacteristic *pCharacteristic;
bool deviceConnected = false;

const byte BUTTON_PINS[7] = {4, 5, 6, 7, 15, 16, 17};
const byte NOTES[7]       = {36, 38, 42, 46, 48, 41, 49};

bool lastStates[7] = {HIGH, HIGH, HIGH, HIGH, HIGH, HIGH, HIGH};
unsigned long lastTimeChanged[7] = {0,0,0,0,0,0,0};
unsigned long debounceDuration = 10;

uint8_t midiPacket[5] = {0x80, 0x80, 0x00, 0x00, 0x00};

class MyServerCallbacks: public BLEServerCallbacks {
    void onConnect(BLEServer* pServer) { deviceConnected = true; }
    void onDisconnect(BLEServer* pServer) { deviceConnected = false; }
};

void setup() {
  Serial.begin(115200);

  for (int i=0; i<7; i++) {
    pinMode(BUTTON_PINS[i], INPUT_PULLUP);
  }

  BLEDevice::init("ESP32-MIDI");
  BLEServer *pServer = BLEDevice::createServer();
  pServer->setCallbacks(new MyServerCallbacks());

  BLEService *pService = pServer->createService(SERVICE_UUID);
  pCharacteristic = pService->createCharacteristic(
    CHARACTERISTIC_UUID,
    BLECharacteristic::PROPERTY_READ   |
    BLECharacteristic::PROPERTY_WRITE  |
    BLECharacteristic::PROPERTY_NOTIFY |
    BLECharacteristic::PROPERTY_WRITE_NR
  );
  pCharacteristic->addDescriptor(new BLE2902());
  pService->start();

  BLEAdvertising *pAdvertising = pServer->getAdvertising();
  pAdvertising->addServiceUUID(pService->getUUID());
  pAdvertising->start();
}

void handleButton(int index) {
  byte state = digitalRead(BUTTON_PINS[index]);
  if (millis() - lastTimeChanged[index] >= debounceDuration) {
    if (state != lastStates[index]) {
      lastTimeChanged[index] = millis();
      lastStates[index] = state;

      if (state == LOW) {
        midiPacket[2] = 0x90;
        midiPacket[3] = NOTES[index];
        midiPacket[4] = 127;
      } else {
        midiPacket[2] = 0x80;
        midiPacket[3] = NOTES[index];
        midiPacket[4] = 0;
      }
      pCharacteristic->setValue(midiPacket, 5);
      pCharacteristic->notify();
    }
  }
}

void loop() {
  if (!deviceConnected) return;
  for (int i=0; i<7; i++) {
    handleButton(i);
  }
}
